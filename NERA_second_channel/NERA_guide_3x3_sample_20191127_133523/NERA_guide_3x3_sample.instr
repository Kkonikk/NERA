/*******************************************************************************
* Instrument: NERA_guide_3x3_sample.instr
*
* Written by: Peter Konik and Yurii Kireenko
* Date: 27.11.19
* Origin: PNPI
*
* new NERA TOF-spectrometer guide
*
* IBR-2M guide for channel 2
*
* source_lambda_min:   [AA] minimal wavelength produced by the source
* source_lambda_max:   [AA] maximal wavelength produced by the source
* source_optics_dist:      [m] distance between moderator and optics
* guide_widtht:               [m] Initial guide width
* guide_height:               [m] Initial guide height
*focusing_length:           [m] Length of the focusing nose
*linh:                              [m] Distance from the focusing nose to the left focus in vertical plane
*louth:                            [m] Distance from the focusing nose to the right focus in vertical plane
*linw:                              [m] Distance from the focusing nose to the left focus in horizontal plane
*lputw:                           [m] Distance from the focusing nose to the right focus in horizontal plane
*sample_size                   [m] Sample width and height
* ...
*
* %E
*******************************************************************************/
DEFINE INSTRUMENT Nera (source_lambda_min = 0.5, source_lambda_max = 1, guide_width = 0.15, guide_height = 0.26, focusing_length = 10, linh = 10.5, louth = 0.5, linw = 10.5, loutw = 0.5, sample_size = 0.03)

DECLARE
%{
//Source description
double source_I = 1e16;
double source_height=0.4, source_width=0.3;
double lambda0, dlambda;

//Shutter description
double shutter_height = 0.42;
double shutter_width1 = 0.22, shutter_dist1 = 1.17;
double shutter_width2 = 0.30, shutter_dist2 = 1.80;
double shutter_width3 = 0.36, shutter_dist3 = 2.16;
double shutter_width4 = 0.46, shutter_dist4 = 2.56;


//Guide description
double source_optics_dist = 4.2;
double total_length = 105;
double m_side = 6, m_top = 6;
double distance_before_sample = 0.35;
double guide_length_st;

%}

INITIALIZE
%{

lambda0=(source_lambda_min + source_lambda_max)/2;
dlambda=source_lambda_max - lambda0;

total_length = total_length - source_optics_dist - distance_before_sample;
guide_length_st = total_length-focusing_length;

%}

TRACE

COMPONENT origin = Progress_bar()
AT (0, 0, 0) RELATIVE ABSOLUTE

COMPONENT Source_simple = Source_simple(
    yheight=source_height, 
    xwidth=source_width, 
    dist=source_optics_dist, 
    focus_xw=guide_width, 
    focus_yh=guide_height, 
    lambda0=lambda0, 
    dlambda=dlambda, 
    flux=source_I)
AT (0, 0, 0) RELATIVE origin

COMPONENT slit1 = Slit(
    xwidth=shutter_width1, 
    yheight=shutter_height)
AT (0, 0, shutter_dist1) RELATIVE origin

COMPONENT slit2 = Slit(
    xwidth=shutter_width2, 
    yheight=shutter_height)
AT (0, 0, shutter_dist2) RELATIVE origin

COMPONENT slit3 = Slit(
    xwidth=shutter_width3, 
    yheight=shutter_height)
AT (0, 0, shutter_dist3) RELATIVE origin

COMPONENT slit4 = Slit(
    xwidth=shutter_width4, 
    yheight=shutter_height)
AT (0, 0, shutter_dist4) RELATIVE origin

COMPONENT Guide_start_arm = Arm()
AT (0, 0, source_optics_dist) RELATIVE origin

COMPONENT  Main_guide = Guide_gravity(
    w1 = guide_width, h1 = guide_height,  l = guide_length_st, mleft  = 6, mright = 6, 
mtop = 6, mbottom = 6)
  AT (0, 0, 0) RELATIVE Guide_start_arm

COMPONENT Main_guide_arm = Arm()
AT (0, 0, guide_length_st) RELATIVE Main_guide

COMPONENT Focusing_nose = Guide_tapering(
    option="elliptical", 
    w1=guide_width, 
    h1=guide_height, 
    l=focusing_length, 
    linh = linh, 
    louth = louth,
    linw = linw,
    loutw = loutw,
    mx=m_side,
    my=m_top)
AT (0, 0, 0.001) RELATIVE Main_guide_arm

COMPONENT guide_end = Arm()
AT (0, 0, focusing_length) RELATIVE Focusing_nose

COMPONENT monitor_nd_xy = Monitor_nD(
    xwidth=sample_size, yheight=sample_size, bins=100, restore_neutron=1, 
    options="dx limits = [-2 2] dy limits = [-2 2]")
AT (0, 0, distance_before_sample) RELATIVE guide_end

FINALLY
%{

%}

END
